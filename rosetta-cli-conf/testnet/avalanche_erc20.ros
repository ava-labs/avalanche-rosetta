create_account(1){
  create{
    network = {"network":"testnet", "blockchain":"Avalanche"};
    key = generate_key({"curve_type": "secp256k1"});
    account = derive({
      "network_identifier": {{network}},
      "public_key": {{key.public_key}}
    });

    // If the account is not saved, the key will be lost!
    save_account({
      "account_identifier": {{account.account_identifier}},
      "keypair": {{key}}
    });
  }
}

simple_transfer_erc20(1){
  transfer{
    transfer.network = {"network":"testnet", "blockchain":"Avalanche"};
    avax_currency = {"symbol":"AVAX", "decimals":18};

    currency = {"symbol":"DAI.e", "decimals":18, "metadata": {"contractAddress":"0x2F10B211817694a2fa00c6b5481Ac4A95b896643"}};
    sender = find_balance({
      "minimum_balance":{
        "value": "50",
        "currency": {{currency}}
      },
      "account_identifier":{"address":"0xA4A4aea84128aDbc5AE9ca7Ff24e3C4bAe7DC41F"}
    });
    unused = find_balance({
      "account_identifier": {{sender.account_identifier}},
      "minimum_balance":{
        "value": "47",
        "currency": {{avax_currency}}
      }
    });

    // Set the recipient_amount as some value <= sender.balance-max_fee
    max_fee = "47";
    available_amount = {{sender.balance.value}} - {{max_fee}};
    recipient_amount = random_number({"minimum": "1", "maximum": {{available_amount}}});
    print_message({"recipient_amount":{{recipient_amount}}});

    // Find recipient and construct operations
    sender_amount = 0 - {{recipient_amount}};
    recipient = find_balance({
      "not_account_identifier":[{{sender.account_identifier}}],
      "minimum_balance":{
        "value": "0",
        "currency": {{currency}}
      },
      "create_limit": 100,
      "create_probability": 50
    });
    transfer.confirmation_depth = "1";
    transfer.operations = [
      {
        "operation_identifier":{"index":0},
        "type":"ERC20_TRANSFER",
        "account":{{sender.account_identifier}},
        "amount":{
          "value":{{sender_amount}},
          "currency":{{currency}}
        }
      },
      {
        "operation_identifier":{"index":1},
        "related_operations":[{"index":0}],
        "type":"ERC20_TRANSFER",
        "account":{{recipient.account_identifier}},
        "amount":{
          "value":{{recipient_amount}},
          "currency":{{currency}}
        }
      }
    ];
  }
}
